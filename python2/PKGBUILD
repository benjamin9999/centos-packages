pkgbase=rqdq-python2
pkgname=(rqdq-python2 rqdq-python2-libs rqdq-python2-devel)
pkgver=2.7.10
pkgrel=7
pkgdesc='An interpreted, interactive, object-oriented programming language'
url='http://python.org/'
makedepends=('zlib-devel' 'openssl-devel' 'libffi-devel' 'readline-devel' 'bzip2-devel')
source=(http://www.python.org/ftp/python/${pkgver}/Python-${pkgver}.tgz)
md5sums=('d7547558fd673bd9d38e2108c6b42521')

_prefix="opt/rqdq"

build() {
  cd "Python-${pkgver}"
  ./configure --enable-shared --prefix=/${_prefix}
  make -j2

  mkdir -p "${srcdir}/build"
  DESTDIR="${srcdir}/build" make install
}

package_rqdq-python2() {
  #depends=("${pkgbase}-libs=${pkgver}")
  depends=("${pkgbase}-libs = ${pkgver}-${pkgrel}.${RPM_DIST}")

  local src="${srcdir}/build/${_prefix}"
  local dest="${pkgdir}/${_prefix}"

  mkdir -p $dest/bin
  mkdir -p $dest/share

  cp -a $src/bin/{pydoc,python,python2,python2.7} $dest/bin/
  cp -a $src/share/man $dest/share/
}

package_rqdq-python2-libs() {
  depends=('zlib' 'openssl', 'libffi' 'readline' 'bzip2')
  install='python2-libs.install'

  local src="${srcdir}/build/${_prefix}"
  local dest="${pkgdir}/${_prefix}"

  mkdir -p $dest/lib $dest/include/python2.7
  cp -a "$src/include/python2.7/pyconfig.h" "$dest/include/python2.7/pyconfig.h"
  cp -a "$src/lib/libpython2.7.so.1.0" "$dest/lib/"
  cp -a "$src/lib/python2.7" "$dest/lib/"

  mkdir -p "${pkgdir}/etc/ld.so.conf.d"
  echo "/${_prefix}/lib" > "${pkgdir}/etc/ld.so.conf.d/${pkgname}.conf"
}

package_rqdq-python2-devel() {
  depends=("${pkgbase}-libs = ${pkgver}-${pkgrel}.${RPM_DIST}")

  mkdir -p "${pkgdir}/${_prefix}"/{bin,include,lib}
  cp -a "${srcdir}/build/${_prefix}/bin"/python*-config "${pkgdir}/${_prefix}/bin/"
  cp -a "${srcdir}/build/${_prefix}/include/python2.7" "${pkgdir}/${_prefix}/include/"
  cp -a "${srcdir}/build/${_prefix}/lib/libpython2.7.so" "${pkgdir}/${_prefix}/lib/"
}
