pkgbase=rqdq-libxml2
pkgname=(rqdq-libxml2 rqdq-libxml2-devel rqdq-libxml2-python)
pkgver=2.9.2
pkgrel=2
makedepends=('zlib-devel' 'rqdq-python2-devel')
source=(ftp://xmlsoft.org/libxml2/libxml2-${pkgver}.tar.gz \
        fix-duplicate-qname-message.patch)
md5sums=('9e6a9aca9d155737868b3dc5fd82f788'
         'a143a60f05d3c2c326270aa4ab01fa1e')

_prefix="opt/rqdq"

prepare() {
  cd "libxml2-${pkgver}"
  patch -p1 < "${srcdir}/fix-duplicate-qname-message.patch"
}

build() {
  cd "libxml2-${pkgver}"
  ./configure --prefix=/$_prefix --with-python=/${_prefix}/bin/python
  make -j2
}

package_rqdq-libxml2() {
  install=libxml2.install
  depends=('zlib')

  cd "libxml2-${pkgver}"
  DESTDIR="$pkgdir" make install
  mkdir -p "${pkgdir}/etc/ld.so.conf.d"
  echo "/${_prefix}/lib" > "${pkgdir}/etc/ld.so.conf.d/${pkgname}.conf"

  fpmx "$pkgname" \
    ${_prefix}/bin/xmlcatalog \
    ${_prefix}/bin/xmllint \
    ${_prefix}/lib/libxml2.so.2 \
    ${_prefix}/lib/libxml2.so.2.9.2 \
    ${_prefix}/share/man/man1/xmlcatalog.1 \
    ${_prefix}/share/man/man1/xmllint.1 \
    ${_prefix}/share/man/man3
}

package_rqdq-libxml2-devel() {
  depends=("${pkgbase}==${pkgver}-${pkgrel}")

  cd "libxml2-${pkgver}"
  DESTDIR="$pkgdir" make install

  fpmx "${pkgname}" \
    ${_prefix}/bin/xml2-config \
    ${_prefix}/include/libxml2 \
    ${_prefix}/lib/libxml2.so \
    ${_prefix}/lib/pkgconfig \
    ${_prefix}/lib/xml2Conf.sh \
    ${_prefix}/share/aclocal/libxml.m4 \
    ${_prefix}/share/doc/libxml2-2.9.2 \
    ${_prefix}/share/gtk-doc \
    ${_prefix}/share/man/man1/xml2-config.1
}

package_rqdq-libxml2-python() {
  depends=("${pkgbase}==${pkgver}-${pkgrel}" 'rqdq-python2-libs>=2.7.10')

  cd "libxml2-${pkgver}"
  DESTDIR="$pkgdir" make install

  fpmx "${pkgname}" \
    ${_prefix}/lib/python2.7 \
    ${_prefix}/share/doc/libxml2-python-2.9.2
}
