pkgbase=rqdq-libxml2
pkgname=(rqdq-libxml2 rqdq-libxml2-devel rqdq-libxml2-python)
pkgver=2.9.2
pkgrel=8
pkgdesc='Library providing XML and HTML support'
url='http://xmlsoft.org/'
license=('MIT')
makedepends=('zlib-devel' 'rqdq-python2-devel')
source=(ftp://xmlsoft.org/libxml2/libxml2-${pkgver}.tar.gz \
        fix-duplicate-qname-message.patch)
md5sums=('9e6a9aca9d155737868b3dc5fd82f788'
         'a143a60f05d3c2c326270aa4ab01fa1e')

_prefix="opt/rqdq"

prepare() {
  cd "libxml2-${pkgver}"
  patch -p1 < "${srcdir}/fix-duplicate-qname-message.patch"
}

build() {
  cd "libxml2-${pkgver}"
  PATH=/opt/rqdq/bin:$PATH ./configure \
    --with-python=/${_prefix}/bin/python \
    --prefix=/${_prefix}
  LIBRARY_PATH=/opt/rqdq/lib:$LIBRARY_PATH make -j2

  mkdir -p "${srcdir}/build"
  DESTDIR="${srcdir}/build" make install
}

package_rqdq-libxml2() {
  install=libxml2.install
  depends=('zlib')

  local _src="${srcdir}/build/${_prefix}"
  local _dst="${pkgdir}/${_prefix}"

  cd "libxml2-${pkgver}"

  install -d "$_dst"/{bin,lib,share/man/man1}
  cp -a "$_src"/bin/{xmlcatalog,xmllint} "$_dst"/bin/
  cp -a "$_src"/lib/libxml2.so.* "$_dst"/lib/
  cp -a "$_src"/share/man/man1/{xmlcatalog.1,xmllint.1} "$_dst"/share/man/man1/
  cp -a "$_src"/share/man/man3 "$_dst"/share/man/

  install -d "${pkgdir}/etc/ld.so.conf.d"
  echo "/${_prefix}/lib" > "${pkgdir}/etc/ld.so.conf.d/${pkgname}.conf"
}

package_rqdq-libxml2-devel() {
  depends=("${pkgbase} = ${pkgver}-${pkgrel}.${RPM_DIST}")

  local _src="${srcdir}/build/${_prefix}"
  local _dst="${pkgdir}/${_prefix}"

  cd "libxml2-${pkgver}"

  install -d "$_dst"/{bin,include,lib,share/{doc,man/man1}}
  cp -a "$_src"/bin/xml2-config "$_dst"/bin/
  cp -a "$_src"/include/libxml2 "$_dst"/include/
  cp -a "$_src"/lib/{libxml2.so,pkgconfig,xml2Conf.sh} "$_dst"/lib/
  cp -a "$_src"/share/aclocal "$_dst"/share/
  cp -a "$_src"/share/doc/libxml2-${pkgver} "$_dst"/share/doc/
  cp -a "$_src"/share/gtk-doc "$_dst"/share/
  cp -a "$_src"/share/man/man1/xml2-config.1 "$_dst"/share/man/man1/
}

package_rqdq-libxml2-python() {
  depends=("${pkgbase} = ${pkgver}-${pkgrel}.${RPM_DIST}" 'rqdq-python2-libs = 2.7.11')

  local _src="${srcdir}/build/${_prefix}"
  local _dst="${pkgdir}/${_prefix}"

  cd "libxml2-${pkgver}"

  install -d "$_dst"/{lib,share/doc}
  cp -a "$_src"/lib/python2.7 "$_dst"/lib/
  cp -a "$_src"/share/doc/libxml2-python-${pkgver} "$_dst"/share/doc/
}
