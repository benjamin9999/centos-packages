pkgbase=rqdq-libxslt
pkgname=(rqdq-libxslt rqdq-libxslt-devel rqdq-libxslt-python)
pkgver=1.1.28
pkgrel=10
pkgdesc='Library providing the Gnome XSLT Engine'
url='http://xmlsoft.org/'
license=('MIT')
makedepends=('rqdq-libxml2-devel' 'rqdq-libxml2-python' 'rqdq-python2' 'rqdq-python2-devel')
source=(ftp://xmlsoft.org/XSLT/libxslt-${pkgver}.tar.gz)
md5sums=('9667bf6f9310b957254fdcf6596600b7')

_prefix="opt/rqdq"

build() {
  cd "libxslt-${pkgver}"
  PATH=/opt/rqdq/bin:$PATH ./configure \
    --with-python=/${_prefix}/bin/python \
    --with-libxml-prefix=/${_prefix} \
    --prefix=/${_prefix}
  LIBRARY_PATH=/opt/rqdq/lib:$LIBRARY_PATH make -j2

  mkdir -p "${srcdir}/build"
  DESTDIR="${srcdir}/build" make install
}

package_rqdq-libxslt() {
  depends=('zlib' "rqdq-libxml2 = 2.9.2-8.${RPM_DIST}")
  install=libxslt.install

  local _src="${srcdir}/build/${_prefix}"
  local _dst="${pkgdir}/${_prefix}"

  cd "libxslt-${pkgver}"

  install -d "$_dst"/{bin,lib,share/man}
  cp -a "$_src"/bin/xsltproc "$_dst"/bin/
  cp -a "$_src"/lib/{libexslt.so.0*,libxslt.so.1*} "$_dst"/lib/
  cp -a "$_src"/share/man/man1 "$_dst"/share/man/

  install -d "${pkgdir}/etc/ld.so.conf.d"
  echo "/${_prefix}/lib" > "${pkgdir}/etc/ld.so.conf.d/${pkgname}.conf"
}

package_rqdq-libxslt-devel() {
  depends=("${pkgbase} = ${pkgver}-${pkgrel}.${RPM_DIST}")

  local _src="${srcdir}/build/${_prefix}"
  local _dst="${pkgdir}/${_prefix}"

  cd "libxslt-${pkgver}"

  install -d "$_dst"/{bin,include,lib,share/{doc,man}}
  cp -a "$_src"/bin/xslt-config "$_dst"/bin/
  cp -a "$_src"/include/{libexslt*,libxslt*} "$_dst"/include/
  cp -a "$_src"/lib/{libexslt.{a,so},libxslt.{a,so},pkgconfig,xsltConf.sh} "$_dst"/lib/
  cp -a "$_src"/share/aclocal "$_dst"/share/
  cp -a "$_src"/share/doc/libxslt-$pkgver "$_dst"/share/doc/
  cp -a "$_src"/share/man/man3 "$_dst"/share/man/
}

package_rqdq-libxslt-python() {
  depends=("${pkgbase} = ${pkgver}-${pkgrel}.${RPM_DIST}" 'rqdq-python2-libs = 2.7.11')

  local _src="${srcdir}/build/${_prefix}"
  local _dst="${pkgdir}/${_prefix}"

  cd "libxslt-${pkgver}"

  install -d "$_dst"/{lib,share/doc}
  cp -a "$_src"/lib/python2.7 "$_dst"/lib/
  cp -a "$_src"/share/doc/libxslt-python-${pkgver} "$_dst"/share/doc/
}
